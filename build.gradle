/*
 *    Copyright 2016 Jonathan Beaudoin
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

buildscript {
    ext.your_github_name = 'Jonatino'
    ext.your_name = 'Jonathan Beaudoin'
    ext.your_email = 'jonathanbeaudoin1996@gmail.com'

    ext.project_name = 'Xena'
    ext.project_description = 'Lightweight, lighting-fast Java Based Cross-Platform CSGO Cheat'
    ext.project_group = 'com.github.'
    ext.project_artifact = 'Xena'
    ext.project_version = '1.2'

    ext.jna_version = '4.3.0-SNAPSHOT'
    ext.kotlin_version = '1.0.3'
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

allprojects {
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java'
    apply plugin: 'application'
}

group project_group
version project_version

mainClassName = "com.github.jonatino.Main"

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId project_group
            artifactId project_artifact
            version project_version
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name project_name
                    description project_description
                    url "https://github.com/$your_github_name/$project_name"
                    scm {
                        url "https://github.com/$your_github_name/$project_name"
                        connection "scm:git:git://github.com/$your_github_name/${project_name}.git"
                        developerConnection "scm:git:ssh:git@github.com:$your_github_name/${project_name}.git"
                    }
                    licenses {
                        license {
                            name 'Apache License 2.0'
                            url 'https://raw.githubusercontent.com/$your_github_name/$project_name/master/LICENSE'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id your_github_name
                            name your_name
                            email your_email
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['maven']
    pkg {
        repo = 'maven'
        name = project_name
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
        websiteUrl = "https://github.com/$your_github_name/$project_name"
        issueTrackerUrl = "https://github.com/$your_github_name/$project_name/issues"
        vcsUrl = "https://github.com/$your_github_name/${project_name}.git"
        githubRepo = "$your_github_name/$project_name"
        version {
            name = project_version
            vcsTag = project_version
            gpg {
                sign = true
            }
        }
    }
}

repositories {
    mavenCentral()

    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "com.github.jonatino:Abendigo-Offset-Scanner:1.6.3"
    compile "org.projectlombok:lombok:1.16.10"
}